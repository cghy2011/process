dnl Initialise autoconf

AC_INIT(src/process.cpp)
AM_CONFIG_HEADER(config.h)

dnl Initialise automake

AM_INIT_AUTOMAKE(process, 0.0.0)

dnl Need libtool

AM_PROG_LIBTOOL

dnl Test for C++ compiler

AC_PROG_CC
AC_PROG_CXX

CXXFLAGS="$CXXFLAGS -Wall"

AC_LANG_CPLUSPLUS
AC_REQUIRE_CPP

dnl Test for WorldForge Atlas Libraries

ATLAS_VERSION=0.4.4
AM_PATH_ATLAS($ATLAS_VERSION)
CPPFLAGS="$CPPFLAGS $ATLAS_CFLAGS"
LIBS="$LIBS $ATLAS_LIBS"

AM_PATH_SIGC(1.0.2, ,
	AC_MSG_ERROR(Couldn't find libsigc++ config or config failed)
)
CPPFLAGS="$CPPFLAGS $SIGC_CFLAGS"
LDFLAGS="$LDFLAGS $SIGC_LIBS"

dnl allow the user to provide the directory where python is installed in
dnl
AC_CACHE_CHECK(for location of python, python_prefix,
[
	python_prefix=/usr
	AC_ARG_WITH(python,
	[ --with-python=DIR          directory python is installed in [default=/usr]],
	[
		if test $withval != yes; then
			python_prefix=$withval
		fi
	])
])

dnl then check for the header file Python.h and set
dnl python_include_path and python_version
dnl appropriately to what we have found
dnl

AC_CHECK_HEADER(python2.2/Python.h,
	[ python_include_path=-I${python_prefix}/include/python2.2
	  python_version=python2.2
        ],[
	AC_CHECK_HEADER(python2.1/Python.h,
		[ python_include_path=-I${python_prefix}/include/python2.1
		  python_version=python2.1
       		],[
		AC_CHECK_HEADER(python2.0/Python.h,
			[ python_include_path=-I${python_prefix}/include/python2.0
			  python_version=python2.0
			],[
			AC_CHECK_HEADER(python1.6/Python.h, 
				[ python_include_path=-I${python_prefix}/include/python1.6
				  python_version=python1.6
        	                ],[
				AC_CHECK_HEADER(python1.5/Python.h, 
					[ python_include_path=-I${python_prefix}/include/python1.5
					  python_version=python1.5
	                                ])
				])
			])
		])
	])

AC_CHECK_LIB(dl,main)
AC_CHECK_LIB(pthread,main)

PYTHON_INCLUDES=${python_include_path}
CPPFLAGS="$CPPFLAGS ${PYTHON_INCLUDES}"

dnl build the library path from the found version
python_lib_path=${python_prefix}/lib/${python_version}/config

AC_CHECK_LIB(${python_version},
    Py_Initialize,
    [
        python_libs="-L${python_lib_path} -l${python_version} -lutil"
    ],,
    [-L${python_lib_path} -lutil]
)

PYTHON_LIBS=${python_libs}
LIBS="$LIBS $PYTHON_LIBS"

dnl ... done with python checks
dnl -----------------------------------------------------------------

dnl Check for libskstream library
AM_PATH_SKSTREAM(0.2.2, ,
	AC_MSG_ERROR(Couldn't find skstream or version is insufficent)
)
CPPFLAGS="$CPPFLAGS $SKSTREAM_CFLAGS"
LIBS="$LIBS $SKSTREAM_LIBS"

AM_PATH_CPPUNIT(1.6.1, ,
    [
	AC_MSG_ERROR(Couldn't Find CppUnit)
    ]
)
CPPFLAGS="$CPPFLAGS $CPPUNIT_CFLAGS"
LIBS="$LIBS $CPPUNIT_LIBS"


dnl Generate files
AC_OUTPUT([
	Makefile
	src/Makefile
])


